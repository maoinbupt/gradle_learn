import com.sohu.sohuvideo.HelloGradle

apply plugin: 'com.android.application'
apply plugin: HelloGradle

android {
//    compileSdkVersion 21
//    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "cn.bupt.newproject0819"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true // 移除没用到的资源和代码
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //第三方 progaurd 文件, 区分存储
//            proguardFile "proguard-rxjava.pro"
//            proguardFile "proguard-realm.pro"
//            proguardFile "proguard-retrofit.pro"
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }

        free {  // 对 src/free/ source set 进行配置 (flavor)
        }
        release { // 对 src/release/ source set 进行配置 (buildType)
        }
        freeFlyme {  // 对 src/freeFlyme/ 额外 source set 进行配置
        }
    }


    flavorDimensions "channel", "type" // flavor 维度声明
    productFlavors {
        free {
            dimension "type"   // type 维度的 free flavor
            applicationIdSuffix ".free"  // 免费版包名后缀 .free
        }
        utlra {
            dimension "type"
            applicationIdSuffix ".ultra" // 旗舰版包名后缀 .ultra
        }
        flyme{  //flyme渠道
            dimension "channel"
        }
        yyb{    //应用宝渠道
            dimension "channel"
        }

    }
}

/**
 * 通过gradle extension方式与插件通信
 * 这样插件就可以通过调用者设置的值来执行task
 * */
greeting{
    message = 'hello in MainProject'
    greeter = 'MainProject caller'
}

/**
 * 通过gradle container方式与插件通信
 * 这样插件就可以对调用者一一say hey
 * */
greetings{
    GaoFeng{
        message "Hey"
    }
    GaoFeng2{
        message "Hey","again!"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/common-lib-net.jar')
    compile files('libs/androidcommon-toolbox.jar')
    compile ('com.squareup.retrofit2:converter-gson:2.1.0')
    compile 'com.android.support:appcompat-v7:21.0.3'
}
